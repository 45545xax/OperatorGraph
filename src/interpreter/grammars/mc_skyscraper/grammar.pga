axiom Box Skyscraper;

terminal Column1 (1,0);
terminal Column2 (1,1);
terminal Column3 (1,2);
terminal Heading (1,3);
terminal Roof (1,4);
terminal Wall1 (1,5);
terminal Wall2 (1,6);
terminal Wall3 (1,7);
terminal Window1 Window2 Window3 Window4 (1,8);

// ---

Skyscraper = RandomRule(ShapeSeed()) {
    0.5: SB |
    0.5: B
};

SB = SubDiv(Y) { -1: B | 3: Scale(-0.8,-1,-0.8) B };

B = RandomRule(ShapeSeed()) {
    0.8: HM |
    0.2: M
};

HM = SubDiv(Y) { -1: M | 1: H };

H = SubDiv(Y) { -4: Scale(-1.0666, -1, -1.0666) Heading | -1: Scale(-1.0666, -1, -1.1333) Heading };

M = CompSplit() { 
    Roof |
    Discard() |
    Sd
};

Sd = RandomRule(ShapeCustomAttribute()) {
    0.8: CFc |
    0.2: FFc
};

CFc = Repeat(X, 5, ADJUST_TO_FILL) SubDiv(X) { 1.25: Cl | -1: Fc1 | 1.25: Cl };

FFc = Repeat(X, 2.5, ADJUST_TO_FILL) Fc2;

Cl = Extrude(0.333) Translate(0,0.1666,0) Cs;

Cs = RandomRule(ShapeCustomAttribute()) {
    0.333: Column1 |
    0.333: Column2 |
    0.333: Column3
};

Fc1 = Repeat(Y, 3, ANCHOR_TO_END) Ts1;

Ts1 = RandomRule(ShapeCustomAttribute())  {
    0.25: T1 |
    0.25: T2 |
    0.25: T3 |
    0.25: T4
};

Fc2 = Repeat(Y, 3, ANCHOR_TO_END) Ts2;

Ts2 = RandomRule(ShapeCustomAttribute())  {
    0.333: T1 |
    0.333: T2 |
    0.334: T3
};

T1 = SubDiv(X) {
    -1: Wall1 |
    2: W1 |
    -1: Wall1
};

W1 = SubDiv(Y) {
  -1: Wall1 |
  1.5: Window1 |
  -1: Wall1
};

T2 = SubDiv(X) {
    -1: Wall1 |
    0.85: W2 |
    0.3: Extrude(0.3) Wall2 |
    0.85: W2 |
    -1: Wall1
};

W2 = SubDiv(Y) {
  -1: Wall1 |
  0.7: Window2 |
  0.1: Wall1 |
  0.7: Window2 |
  -1: Wall1
};

T3 = SubDiv(X) {
    -1: Wall1 |
    0.4: W3 |
    0.1: Wall1 |
    1: W3 |
    0.1: Wall1 |
    0.4: W3 |
    -1: Wall1
};

W3 = SubDiv(Y) {
    -1: Wall1 |
    1.25: Window3 |
    -1: Wall1
};

T4 = SubDiv(Y) {
    1: Scale(-1,-1,0.3) Wall3 |
    -1: Window4
};
