axiom Box A;

terminal WindowSill (1, 0);
terminal BalconyDoor  (1, 1);
terminal Door (1, 2);
terminal BalconyWall (1, 3);
terminal Wall (1, 4);
terminal Window (1, 5);
terminal Roof (1, 6);
terminal RoofSide (1, 7);
terminal BalconyFloor (1, 8);

// ==================================================

A = SubDiv (Y) { -0.6 : Base | -0.4:  MidFloors | 2: RoofStructure};

// duplicate main body of building to save on wall geometry
Base = SubDiv (Y) { -0.5: Translate(0, Exp(ShapeSize(Y) /  2), 0) Scale(-1, -2, -1) Base1 | 
                      -0.5: Translate(0, Exp(ShapeSize(Y) / -2), 0) Scale(-1, -2, -1) WallDummy };

Base1 = SubDiv (Y) { 5: GroundFloor | -1: CompSplit() { Discard() | Discard() | Facade } };

GroundFloor = CompSplit () { Discard() | Discard() | GroundFacade  };
GroundFacade = SubDiv(X) { -1: FacadeGround | 5 :  DoorSection | -1: FacadeGround };
FacadeGround = IfSizeLess(Y, 5) { Discard() | Repeat(Y, 5, ADJUST_TO_FILL) IfSizeLess(X, 5) { Discard() | Repeat(X, 5, ADJUST_TO_FILL) FacadeGround1 } };
FacadeGround1= SubDiv (Y) { -1 : Discard() | 3 : FacadeGround2 | -1 : Discard() };
FacadeGround2 = SubDiv (X) { -1 : Discard() | 3 : Window | -1 : Discard() } ;

DoorSection = Extrude(1) CompSplit() { SubDiv(X) { -1 : Wall | 4: DoorFrame | -1: Wall } | Discard()| Wall };
DoorFrame = SubDiv (X) { -0.2 : Wall | -0.6 : DoorTile | -0.2 : Wall };
DoorTile = SubDiv (Y) { -0.02: Wall | -0.78 : Door | -0.2 : Wall };

// duplicate main body of building to save on wall geometry
MidFloors = SubDiv (Y) { -0.5: Translate(0, Exp(ShapeSize(Y) /  2), 0) Scale(-1, -2, -1) CompSplit() { Discard() | Discard() | Facade } | 
                           -0.5: Translate(0, Exp(ShapeSize(Y) / -2), 0) Scale(-1, -2, -1) WallDummy };

Facade = IfSizeLess(Y, 8) { 
    Discard() | 
    Repeat(Y, 6, ADJUST_TO_FILL) SubDiv(Y) { 1: Discard() | -1: IfSizeLess(X, 8) { Discard() | Repeat(X, 8, ADJUST_TO_FILL) SubDiv(X) { -1: Balcony | -1: FacadeTile1 } } } 
};

FacadeTile1 = SubDiv (Y) { -2 : Discard() | -0.5 : Sill | -2 : FacadeTile2 };
FacadeTile2 = SubDiv (X) { -1 : Discard() | -3 : Window | -1 : Discard() } ;

Sill = SubDiv (X) { -1 : Discard() | -3 : Sill1 | -1 : Discard() } ;
Sill1 = Extrude(0.3) Translate(0,0.15,0) WindowSill;

Balcony = SubDiv(X) {-0.1: Discard() | -0.9: SubDiv(Y) { -1: Balcony1 | -1: BalconyDoorStructure } | -0.1: Discard() };
Balcony1 = Extrude(1.5) Translate(0,0.75,0) CompSplit() { BalconyWall | Discard() | BalconySide };
BalconySide = If(Exp(ShapeNormal(Y) == 1)) { Discard() | If(Exp(ShapeNormal(Y) == -1)) { BalconyFloor | BalconyWall } };

BalconyDoorStructure = Translate(0, Exp(ShapeSize(Y) / -2), 0) Scale(-1, Exp(ShapeSize(Y) * 2), -1) SubDiv(X) { -0.1: Discard() | -0.9: BalconyDoor | -0.1: Discard() };

RoofStructure = SubDiv(Y) { -0.9: CompSplit() { Discard()| Discard() | Wall } | -0.1: RoofStructure1 };

RoofStructure1 = SetAsDynamicConvexRightPrism((0, 0.5),(-0.5, -0.5),(0.5, -0.5)) 
                 Translate(0, 2.4, 0) 
                 Rotate(90, 0, 0)
                 Scale(ShapeSize(X), ShapeSize(Z), 5) 
                 CompSplit() { RoofSide | RoofSide | Roof1 };

Roof1 = If(Exp(ShapeNormal(Y) == -1)) { Discard() | If(Exp(ShapeNormal(X) < 0)) { /* left roof */ Translate(0.5,0,0) Scale(Exp(ShapeSize(X) + 1),-1,-1) Roof | /* right roof */ Translate(-0.5,0,0) Scale(Exp(ShapeSize(X) + 1),-1,-1) Roof } };

WallDummy = Scale(-0.999, -1, -0.999) CompSplit() { Discard() | Discard() | Wall };
